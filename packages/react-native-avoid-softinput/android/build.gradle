import groovy.json.JsonSlurper

buildscript {
  // Buildscript is evaluated before everything else so we can't use getExtOrDefault
  def kotlin_version = rootProject.ext.has('kotlinVersion') ? rootProject.ext.get('kotlinVersion') : project.properties['AvoidSoftinput_kotlinVersion']

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath("com.android.tools.build:gradle:8.5.0")
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "com.diffplug.spotless:spotless-plugin-gradle:6.25.0"
  }
}

// Add this function in case you don't have it already
def isNewArchitectureEnabled() {
  return project.hasProperty("newArchEnabled") && project.newArchEnabled == "true"
}

if (project == rootProject) {
  apply from: 'spotless.gradle'
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

def safeAppExtGet(prop, fallback) {
  def appProject = rootProject.allprojects.find { it.plugins.hasPlugin('com.android.application') }
  appProject?.ext?.has(prop) ? appProject.ext.get(prop) : fallback
}

def resolveReactNativeDirectory() {
  def reactNativeLocation = safeAppExtGet("REACT_NATIVE_NODE_MODULES_DIR", null)
  if (reactNativeLocation != null) {
    return file(reactNativeLocation)
  }

  def reactNativeFromAppNodeModules = file("${projectDir}/../../react-native")
  if (reactNativeFromAppNodeModules.exists()) {
    return reactNativeFromAppNodeModules
  }

  def reactNativeFromProjectNodeModules = file("${rootProject.projectDir}/../node_modules/react-native")
  if (reactNativeFromProjectNodeModules.exists()) {
    return reactNativeFromProjectNodeModules
  }

  def reactNativeFromRootWorkspaceNodeModules = file("${rootProject.projectDir}/../../../node_modules/react-native")
  if (reactNativeFromRootWorkspaceNodeModules.exists()) {
    return reactNativeFromRootWorkspaceNodeModules
  }

  throw new GradleException(
    "[RNAvoidSoftInput] Unable to resolve react-native location in node_modules. You should add project extension property (in `app/build.gradle`) `REACT_NATIVE_NODE_MODULES_DIR` with path to react-native."
  )
}

def getReactNativeVersion() {
  def reactNativeDirectory = resolveReactNativeDirectory()
  def reactNativePackageJsonFile = file("${reactNativeDirectory}/package.json")
  def packageSlurper = new JsonSlurper()
  def reactNativePackageJson = packageSlurper.parseText(reactNativePackageJsonFile.text)
  def reactNativeVersion = reactNativePackageJson.version

  return reactNativeVersion.tokenize('-')[0].tokenize('.')
}

def getReactNativeMinorVersion() {
  List reactNativeVersionSegments = getReactNativeVersion()
  return reactNativeVersionSegments[1].toInteger()
}

def getReactNativeVersionFlavor() {
  int minorVersion = getReactNativeMinorVersion()

  if (minorVersion >= 81) {
    return "reactnative81"
  } else if (minorVersion >= 77) {
    return "reactnative77"
  }
}

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"

  def reactNativeDirectory = resolveReactNativeDirectory()

  react {
    reactNativeDir = file(reactNativeDirectory)
    codegenDir = file("${file(reactNativeDirectory).getParentFile()}/react-native-codegen")
  }
}

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['AvoidSoftinput_' + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties['AvoidSoftinput_' + name]).toInteger()
}

def reactNativeArchitectures() {
  def value = project.getProperties().get("reactNativeArchitectures")
  return value ? value.split(",") : ["armeabi-v7a", "x86", "x86_64", "arm64-v8a"]
}

def supportsNamespace() {
  def parsed = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')
  def major = parsed[0].toInteger()
  def minor = parsed[1].toInteger()

  // Namespace support was added in 7.3.0
  return (major == 7 && minor >= 3) || major >= 8
}

android {
  compileSdkVersion getExtOrIntegerDefault('compileSdkVersion')

  if (supportsNamespace()) {
    namespace "com.reactnativeavoidsoftinput"

    sourceSets {
      main {
        manifest.srcFile "src/main/AndroidManifestNew.xml"
      }
    }
  }

  buildFeatures {
    buildConfig = true
  }

  defaultConfig {
    minSdkVersion 21
    targetSdkVersion getExtOrIntegerDefault('targetSdkVersion')
    versionCode 1
    versionName "1.0"
    buildConfigField "boolean", "IS_NEW_ARCHITECTURE_ENABLED", isNewArchitectureEnabled().toString()
  }

  sourceSets {
    main {
      if (isNewArchitectureEnabled()) {
        java.srcDirs += ['src/newarch/java', "${project.buildDir}/generated/source/codegen/java"]
      } else {
        java.srcDirs += ['src/oldarch/java']
      }
    }
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable 'GradleCompatible'
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  flavorDimensions "RNAS-RNVersion"
  productFlavors {
    reactnative77 {
      dimension "RNAS-RNVersion"
      buildConfigField("int", "RNAS_RN_V_MINOR", "77")
    }
    reactnative81 {
      dimension "RNAS-RNVersion"
      buildConfigField("int", "RNAS_RN_V_MINOR", "81")
    }
  }

  def flavor = getReactNativeVersionFlavor()
  variantFilter { variant ->
    def names = variant.flavors*.name
    if (!names.contains(flavor)) {
      setIgnore(true)
    }
  }
}

repositories {
  mavenCentral()
  google()
}

def kotlin_version = getExtOrDefault('kotlinVersion')
def kotlin_coroutines_version = getExtOrDefault('kotlinCoroutinesVersion')
def androidX_core_ktx_version = getExtOrDefault('androidXCoreKtxVersion')

dependencies {
  implementation "com.facebook.react:react-android" // Set by the React Native Gradle Plugin
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"
  implementation "androidx.core:core-ktx:$androidX_core_ktx_version"
}
